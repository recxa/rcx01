TITLE:: FluidSpectralShape
SUMMARY:: Seven Spectral Shape Descriptors in Realtime
CATEGORIES:: Libraries>FluidCorpusManipulation
RELATED:: Classes/FluidBufSpectralShape,Classes/FluidPitch,Classes/FluidMelBands,Classes/FluidLoudness,Classes/FluidMFCC,Guides/FluidCorpusManipulation,Classes/SpecCentroid,Classes/SpecFlatness,Classes/SpecPcile
DESCRIPTION::

    
    Seven of the  spectral shape descriptors, computed on a linear scale for both amplitude and frequency.


    
    The descriptors are:

    LIST::
    ## 
    the four first statistical moments (LINK::https://en.wikipedia.org/wiki/Moment_(mathematics)::), more commonly known as:

    LIST::
    ## 
    the spectral centroid (1) in Hertz. This is the point that splits the spectrum in 2 halves of equal energy. It is the weighted average of the magnitude spectrum.

    ## 
    the spectral spread (2) in Hertz. This is the standard deviation of the spectrum envelope, or the average of the distance to the centroid.

    ## 
    the normalised skewness (3) as ratio. This indicates how tilted is the spectral curve in relation to the middle of the spectral frame, i.e. half of the Nyquist frequency. If it is below the frequency of the magnitude spectrum, it is positive.

    ## 
    the normalised kurtosis (4) as ratio. This indicates how focused the spectral curve is. If it is peaky, the value is high.

    ::
    ## 
    the rolloff (5) in Hertz. This indicates the frequency under which 95% of the energy is included.

    ## 
    the flatness (6) in dB. This is the ratio of geometric mean to the magnitude, over the arithmetic mean of the magnitudes. It yields a very approximate measure on how noisy a signal is.

    ## 
    the crest (7) in dB. This is the ratio of the loudest magnitude over the RMS of the whole frame. A high number is an indication of a loud peak poking out from the overall spectral curve.

    ::
    The drawings in Peeters 2003 ( LINK::http://recherche.ircam.fr/anasyn/peeters/ARTICLES/Peeters_2003_cuidadoaudiofeatures.pdf:: ) are useful, as are the commented examples below. For the mathematically-inclined reader, the tutorials and code offered here
    ( LINK::https://www.audiocontentanalysis.org/:: ) are interesting to further the understanding. For examples of the impact of computing the moments in power magnitudes, and/or in exponential frequency scale, please refer to the helpfile.

    The process will return a multichannel control stream with the seven values, which will be repeated if no change happens within the algorithm, i.e. when the hopSize is larger than the signal vector size.


Read more about FluidSpectralShape on the link::https://learn.flucoma.org/reference/spectralshape##learn platform::.

CLASSMETHODS::

METHOD:: kr

ARGUMENT:: in

  Audio-rate signal to analyze








ARGUMENT:: select

    
    An array of CODE::symbols:: indicating which analyses to return. The options are CODE::centroid::, CODE::spread::, CODE::skewness::, CODE::kurtosis::, CODE::rolloff::, CODE::flatness::, and CODE::crest::. If nothing is specified, the object will return all the analyses. The analyses will always appear in their normal order, this argument just allows for a selection of them to be returned. Reordering the options in this argument will not reorder how the analyses are returned.



ARGUMENT:: minFreq

    
    The minimum frequency that the algorithm will consider for computing the spectral shape. Frequencies below will be ignored. The default of 0 goes down to DC when possible.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ::


ARGUMENT:: maxFreq

    
    The maximum frequency that the algorithm will consider for computing the spectral shape. Frequencies above will be ignored. The default of -1 goes up to Nyquist.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::-1::

    ::


ARGUMENT:: rolloffPercent

    
    This sets the percentage of the frame's energy that will be reported as the rolloff frequency. The default is 95%.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ## 
    Maximum: CODE::100::

    ::


ARGUMENT:: unit

    
    The frequency unit for the spectral shapes to be computed upon, and outputted at. The default (0) is in Hertz and computes the moments on a linear spectrum. The alternative is in MIDI note numbers(1), which compute the moments on an exponential spectrum.



ARGUMENT:: power

    
    This flag sets the scaling of the magnitudes in the moment calculation. It uses either its amplitude (0, by default) or its power (1).



ARGUMENT:: windowSize

    
    The window size. As sinusoidal estimation relies on spectral frames, we need to decide what precision we give it spectrally and temporally. For more information visit LINK::https://learn.flucoma.org/learn/fourier-transform/::



ARGUMENT:: hopSize

    
    The window hop size. As sinusoidal estimation relies on spectral frames, we need to move the window forward. It can be any size, but low overlap will create audible artefacts. The -1 default value will default to half of windowSize (overlap of 2).



ARGUMENT:: fftSize

    
    The inner FFT/IFFT size. It should be at least 4 samples long, at least the size of the window, and a power of 2. Making it larger allows an oversampling of the spectral precision. The -1 default value will default to windowSize.



ARGUMENT:: maxFFTSize

    
    Set an explicit upper bound on the FFT size at object instantiation. The default of CODE::nil:: or -1 sets this to whatever the initial FFT size is



 
 

RETURNS::     
    A 7-channel KR signal with the seven spectral shape descriptors. The latency is windowSize.



INSTANCEMETHODS::
  
EXAMPLES::
code::

~src = Buffer.read(s,FluidFilesPath("Nicol-LoopE-M.wav"));

(
~synth = {
	arg power = 1;
	var sig = PlayBuf.ar(1,~src,BufRateScale.ir(~src),loop:1);
	var spec = FluidSpectralShape.kr(sig,[\centroid,\spread],unit:1,power:power);
	var noise = WhiteNoise.ar(1);
	var q = pow(2,spec[1]/6);
	q = pow(q,0.5) / (q-1);

	spec[0].poll(label:"centroid midi");
	spec[1].poll(label:"spread midi");
	spec[0].midicps.poll(label:"centroid hz");
	q.poll(label:"q");
	0.poll(label:"---------------------------");

	noise = BPF.ar(noise,spec[0].midicps.lag(0.1),1);
	noise = noise * FluidLoudness.kr(sig,[\loudness]).dbamp * 10.dbamp;
	Limiter.ar([sig,noise]);
}.play;
)

~synth.set(\power,1);

::
strong::logarithmic scale::
The computation of the spectral centroid can also be done considering a logarithmic pitch scale and the power of the magnitudes. This yields values that are generally considered to be more in line with perception, for instance where the shape is often drawn and described in logarithmic terms, i.e., dB per octave.

Compare the values of the centroid and the spread in both scales. The lower the frequency, the more the linear spectral bias shows. The same applies to the spread. The logarithmic unit is in semitones. To convert, etiher divide by 12 to get the octave of one standard deviation, or divide by 6 to get the width of the filter in octaves. One clear observation is that the width is now in a range that scales with what we hear, growing fourfold as the filter goes from resonanting to more broadband.
code::

(
{
	var freq = MouseX.kr(20,20000,1);
	var q = MouseY.kr(1,100);
	var noise = BPF.ar(PinkNoise.ar,freq,q.reciprocal);
	var lin = FluidStats.kr(FluidSpectralShape.kr(noise,[\centroid,\spread]),40)[0];
	var log = FluidStats.kr(FluidSpectralShape.kr(noise,[\centroid,\spread],unit:1,power:1),40)[0];

	freq.poll(label:"filter frequency ");
	q.poll(label:"filter q         ");
	lin[0].poll(label:"lin centroid     ");
	lin[1].poll(label:"lin spread       ");
	log[0].midicps.poll(label:"log centroid     ");
	log[1].poll(label:"log spread       ");
	0.poll(label:"-------------------------");

	noise.dup;
}.play;
)

::
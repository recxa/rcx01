TITLE:: FluidTransientSlice
SUMMARY:: Transient-Based Realtime Audio Slicer
CATEGORIES:: Libraries>FluidCorpusManipulation
RELATED:: Classes/FluidBufTransientSlice,Classes/FluidAmpSlice,Classes/FluidNoveltySlice,Classes/FluidOnsetSlice,Guides/FluidCorpusManipulation
DESCRIPTION::

    
    A realtime transient-based slice extractor


    
    TransientSlice identifies slice points in a real time signal by implementing a "de-clicking" algorithm based on the assumption that a transient is a sample or series of samples that are anomalous when compared to surrounding samples. It creates a model of the time series of samples, so that when a given sample doesn't fit the model (its "error" or anomalous-ness goes above CODE::threshFwd::) it is determined to be a transient and a slice point is identified.

    The series of samples determined to be a transient will continue until the error goes below CODE::threshBack::, indicating that the samples are again more in-line with the model.

    The process will return an audio stream with single sample impulses at estimated starting points of the different slices.

    The algorithm implemented is from chapter 5 of "Digital Audio Restoration" by Godsill, Simon J., Rayner, Peter J.W. with some bespoke improvements on the detection function tracking.


Read more about FluidTransientSlice on the link::https://learn.flucoma.org/reference/transientslice##learn platform::.

CLASSMETHODS::

METHOD:: ar

ARGUMENT:: in 

  Audio-rate signal to slice








ARGUMENT:: order

    
    The number of previous samples used by the algorithm to create the model of the signal within the CODE::blockSize:: window of analysis. CODE::order:: must be less than CODE::blockSize::.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: MAX(CODE::windowSize, 10::)

    ## 
    Maximum: MIN(CODE::blockSize, 200::)

    ::


ARGUMENT:: blockSize

    
    The size of audio block (in samples) on which the process is operating. This determines the maximum duration (in samples) of a detected transient, which cannot be more than half of CODE::blockSize - order::. High values are more cpu intensive.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: MAX(CODE::order, 100::)

    ## 
    Maximum: CODE::4096::

    ::


ARGUMENT:: padSize

    
    The size (in samples) of analysis on each side of CODE::blockSize:: used to provide some historical context for analysis, so that each CODE::blockSize:: isn't modelled completely independently of its predecessor.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ## 
    Maximum: CODE::1024::

    ::


ARGUMENT:: skew

    
    The nervousness of the bespoke detection function. It ranges from -10 to 10 (it has no units) representing the strength and direction of some nonlinearity applied to the detection signal which controls how peaks are amplified or smoothed before the thresholding. High values increase the sensitivity to small variations.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::-10::

    ## 
    Maximum: CODE::10::

    ::


ARGUMENT:: threshFwd

    
    The threshold applied to the smoothed forward prediction error for determining an onset. The units are roughly in standard deviations, thus can be considered how "deviant", or anomalous, the signal must be to be detected as a transient. It allows tight identification of the start of the anomaly as it proceeds forward.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ::


ARGUMENT:: threshBack

    
    The threshold applied to the smoothed backward prediction error for determining an offset. The units are roughly in standard deviations, thus can be considered how "deviant", or anomalous, the signal must be to be considered transient. When the smoothed error function goes below CODE::threshBack:: an offset is identified. As it proceeds backwards in time, it allows tight identification of the end of the anomaly.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ::


ARGUMENT:: windowSize

    
    The averaging window of the error detection function. It needs smoothing as it is very jittery. The longer the window, the less precise, but the less false positive.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ## 
    Maximum: CODE::order::

    ::


ARGUMENT:: clumpLength

    
    The window size in samples within which anomalous samples will be clumped together to avoid over-detecting in time. This is similar to setting a minimum slice length.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ::


ARGUMENT:: minSliceLength

    
    The minimum duration of a slice in samples.



 
 

INSTANCEMETHODS::
  
EXAMPLES::

CODE::
//load some sounds
~src = Buffer.read(s,FluidFilesPath("Tremblay-AaS-SynthTwoVoices-M.wav"));

// basic param (the process add a latency of (blockSize + padSize - order) samples
(
{
	var sig = PlayBuf.ar(1,~src,BufRateScale.ir(~src),loop:1);
	var order = 20; // this is the default
	var blockSize = 256; // this is the default
	var padSize = 128; // this is the default
	var slice_impulses = FluidTransientSlice.ar(sig,order,blockSize,padSize);
	sig = DelayN.ar(sig, 1, ((blockSize + padSize) - order) * SampleDur.ir); // compute the latency and delay the original signal
	[sig,slice_impulses * -6.dbamp];
}.play
)

// other parameters
(
{
	var sig = PlayBuf.ar(1,~src,BufRateScale.ir(~src),loop:1);
	var order = 80;
	var blockSize = 256; // this is the default
	var padSize = 128; // this is the default
	var slice_impulses = FluidTransientSlice.ar(sig,order,blockSize,padSize,minSliceLength:2205);
	sig = DelayN.ar(sig, 1, ((blockSize + padSize) - order) * SampleDur.ir); // compute the latency and delay the original signal
	[sig,slice_impulses * -6.dbamp];
}.play
)

// More musical, transient-trigged autopan
(
{
	var sig = PlayBuf.ar(1,~src,BufRateScale.ir(~src),loop:1);
	var order = 20; // this is the default
	var blockSize = 256; // this is the default
	var padSize = 128; // this is the default
	var slice_impulses = FluidTransientSlice.ar(sig,order,blockSize,padSize);
	sig = DelayN.ar(sig, 1, ((blockSize + padSize) - order) * SampleDur.ir); // compute the latency and delay the original signal
	Pan2.ar(sig,TRand.ar(-1.0,1.0,slice_impulses).poll);
}.play
)
::

TITLE:: FluidChroma
SUMMARY:: A histogram of pitch classes in Realtime
CATEGORIES:: Libraries>FluidCorpusManipulation
RELATED:: Classes/FluidBufChroma,Classes/FluidPitch,Classes/FluidLoudness,Classes/FluidMFCC,Classes/FluidSpectralShape,Guides/FluidCorpusManipulation
DESCRIPTION::

    
    This class computes a histogram of the energy contained for each pitch class across the analysis frequency range.


    
    Also known as a chromagram, this typically allows you to get a contour of how much each semitone is represented in the spectrum over time. The number of chroma bins (and, thus, pitch classes) and the central reference frequency can be adjusted.

    The process will return a multichannel control stream of size maxNumChroma, which will be repeated if no change happens within the algorithm, i.e. when the hopSize is larger than the signal vector size.


Read more about FluidChroma on the link::https://learn.flucoma.org/reference/chroma##learn platform::.

CLASSMETHODS::

METHOD:: kr

ARGUMENT:: in

  Audio-rate signal to analyze








ARGUMENT:: numChroma

    
    The number of chroma bins per octave. It will determine how many channels are output per input channel.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::2::

    ## 
    Maximum: CODE::maxnumChroma::

    ::


ARGUMENT:: ref

    
    The frequency of reference in Hz for the tuning of the middle A (default: 440 Hz)

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ## 
    Maximum: CODE::22000::

    ::


ARGUMENT:: normalize

    
    This flag enables the scaling of the output. It is off (0) by default. (1) will normalise each frame to sum to 1. (2) normalises each frame relative to the loudest chroma bin being 1.



ARGUMENT:: minFreq

    
    The lower frequency included in the analysis, in Hz.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ::


ARGUMENT:: maxFreq

    
    The highest frequency included in the analysis, in Hz.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::-1::

    ::


ARGUMENT:: windowSize

    
    The window size. As sinusoidal estimation relies on spectral frames, we need to decide what precision we give it spectrally and temporally. For more information visit LINK::https://learn.flucoma.org/learn/fourier-transform/::



ARGUMENT:: hopSize

    
    The window hop size. As sinusoidal estimation relies on spectral frames, we need to move the window forward. It can be any size, but low overlap will create audible artefacts. The -1 default value will default to half of windowSize (overlap of 2).



ARGUMENT:: fftSize

    
    The inner FFT/IFFT size. It should be at least 4 samples long, at least the size of the window, and a power of 2. Making it larger allows an oversampling of the spectral precision. The -1 default value will default to windowSize.



ARGUMENT:: maxFFTSize

    
    Set an explicit upper bound on the FFT size at object instantiation. The default of CODE::nil:: or -1 sets this to whatever the initial FFT size is



ARGUMENT:: maxNumChroma

    
    Manually sets a maximum value for CODE::numChroma::. Can only be set at object instantiation. Default value of -1 sets this to the initial value of CODE::numChroma::



 
 

RETURNS::     
    A  KR signal of maxNumChroma channels, giving the measure amplitudes for each chroma bin. The latency is windowSize.



INSTANCEMETHODS::
  
EXAMPLES::

code::

~src = Buffer.readChannel(s,FluidFilesPath("Tremblay-SA-UprightPianoPedalWide.wav"),channels:[0]);

(
~synth = {
	var sig = PlayBuf.ar(1,~src,BufRateScale.ir(~src),loop:1);
	var chroma = FluidChroma.kr(sig,normalize:1); // normalize = 1 makes the output invariant to the signal loudness
	var sines;
	chroma = FluidStats.kr(chroma,40)[0]; // index 0 to get just the means
	SendReply.kr(Impulse.kr(30),"/chroma",chroma);
	sines = SinOsc.ar((69..80).midicps,0,chroma * -5.dbamp);
	sines = Splay.ar(sines);
	sig.dup + sines;
}.play;

//create a monitoring window for the values

~win = Window("Chroma Monitor", Rect(10, 10, 620, 320)).front;
~ms = MultiSliderView(~win,Rect(10, 10, 600, 300)).elasticMode_(1).isFilled_(1);

OSCdef(\chroma,{
	arg msg;
	defer{
		~ms.value_(msg[3..]);
	};
},"/chroma");
)

::
strong::More than 12 pitch classes::
code::

// dividing the octave (and therefore chroma into 19 equal divisions of the octave:

~src = Buffer.readChannel(s,FluidFilesPath("Tremblay-SA-UprightPianoPedalWide.wav"),channels:[0]);

(
~synth = {
	var sig = PlayBuf.ar(1,~src,BufRateScale.ir(~src),loop:1);
	var chroma = FluidChroma.kr(sig,normalize:1,numChroma:19); // normalize = 1 makes the output invariant to the signal loudness
	var freqs, sines;
	chroma = FluidStats.kr(chroma,40)[0]; // index 0 to get just the means
	SendReply.kr(Impulse.kr(30),"/chroma",chroma);
	freqs = chroma.numChannels.collect{arg i; 440 * 2.pow(i/chroma.numChannels)};
	sines = SinOsc.ar(freqs,0,chroma * -5.dbamp);
	sines = Splay.ar(sines);
	sig.dup + sines;
}.play;

//create a monitoring window for the values

~win = Window("Chroma Monitor", Rect(10, 10, 620, 320)).front;
~ms = MultiSliderView(~win,Rect(10, 10, 600, 300)).elasticMode_(1).isFilled_(1);

OSCdef(\chroma,{
	arg msg;
	defer{
		~ms.value_(msg[3..]);
	};
},"/chroma");
)

::
TITLE:: FluidAmpFeature
SUMMARY:: Realtime Amplitude Differential Feature
CATEGORIES:: Libraries>FluidCorpusManipulation
RELATED:: Classes/FluidAmpGate,Classes/FluidAmpSlice,Classes/FluidOnsetFeature,Classes/FluidNoveltyFeature,Guides/FluidCorpusManipulation,Classes/Amplitude
DESCRIPTION::

    
    Calculate the amplitude differential feature in realtime.


    
    LINK::Classes/FluidAmpSlice:: uses the differential between a fast and a slow envelope follower to determine changes in amplitude. This object calculates the amplitude differential and copies it to an output buffer.


Read more about FluidAmpFeature on the link::https://learn.flucoma.org/reference/ampfeature##learn platform::.

CLASSMETHODS::

METHOD:: ar

ARGUMENT:: in
The audio to be processed.








ARGUMENT:: fastRampUp

    
    The number of samples the relative envelope follower will take to reach the next value when raising. Typically, this will be faster than slowRampUp.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::1::

    ::


ARGUMENT:: fastRampDown

    
    The number of samples the relative envelope follower will take to reach the next value when falling. Typically, this will be faster than slowRampDown.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::1::

    ::


ARGUMENT:: slowRampUp

    
    The number of samples the absolute envelope follower will take to reach the next value when raising.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::1::

    ::


ARGUMENT:: slowRampDown

    
    The number of samples the absolute envelope follower will take to reach the next value when falling.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::1::

    ::


ARGUMENT:: floor

    
    The level in dB the slowRamp needs to be above to consider a detected difference valid, allowing to ignore the slices in the noise floor.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::-144::

    ## 
    Maximum: CODE::144::

    ::


ARGUMENT:: highPassFreq

    
    The frequency of the fourth-order Linkwitzâ€“Riley high-pass filter (LINK::https://en.wikipedia.org/wiki/Linkwitz%E2%80%93Riley_filter::). This is done first on the signal to minimise low frequency intermodulation with very fast ramp lengths. A frequency of 0 bypasses the filter.

    STRONG::Constraints::

    LIST::
    ## 
    Minimum: CODE::0::

    ::


 
 

INSTANCEMETHODS::
  
EXAMPLES::
strong::Use the output as a stream of decibels to manipulate another (or the same) audio signal::
code::
~drums = Buffer.read(s,FluidFilesPath("Nicol-LoopE-M.wav"));

// listen to the original
~drums.play;

// add extra pop to the drums by using the AmpFeature envelope
(
{
	var sig = PlayBuf.ar(~drums.numChannels,~drums,BufRateScale.ir(~drums),loop:1);
	var amp_feature = FluidAmpFeature.ar(sig,100,100,44100,44100,-40);
	amp_feature.poll;
	sig * amp_feature.dbamp * -30.dbamp;
}.play;
)

// really suppress the amplitude peaks by inverting the feature
(
{
	var sig = PlayBuf.ar(~drums.numChannels,~drums,BufRateScale.ir(~drums),loop:1);
	var amp_feature = FluidAmpFeature.ar(sig,100,100,44100,44100,-40) * -1;
	amp_feature.poll;
	sig * amp_feature.dbamp
}.play;
)

::
strong::Make your own threshold, similar to how FluidAmpSlice works::
code::

~src = Buffer.read(s,FluidFilesPath("Nicol-LoopE-M.wav"));

// listen to the original
~src.play;

(
{
	var sig = PlayBuf.ar(~src.numChannels,~src,BufRateScale.ir(~src),loop:1);
	var amp_feature = FluidAmpFeature.ar(sig,100,100,44100,44100,-40);
	var trig = Schmidt.ar(amp_feature,0,6);
	SendReply.ar(trig,"/amp_onset");
	sig;
}.play;

OSCdef(\amp_onset,{
	arg msg;
	"onset".postln;
},"/amp_onset");
)
::